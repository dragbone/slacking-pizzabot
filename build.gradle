import org.ajoberstar.grgit.Grgit

import java.text.SimpleDateFormat

group 'pizzaslackbot'
version 'alpha'

buildscript {
    ext.kotlin_version = '1.2.0'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.ajoberstar:grgit:1.1.0'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

task versionTxt() {
    doLast {
        new File("$projectDir/src/main/resources/version.properties").text = """Version = $version
Revision = ${Grgit.open(dir: '.').head().id}
BuildTime = ${new SimpleDateFormat("dd-MM-yyyy HH:mm:ss").format(new Date())}
ApplicationName = $rootProject.name"""
    }
}

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile "com.ullink.slack:simpleslackapi:1.2.0"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.7.5"
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
}

mainClassName = "com.dragbone.pizzabot.RunnerKt"

processResources.dependsOn(versionTxt)

sourceSets.main.resources { srcDirs = ["src/main/resources"] }

jar {
    manifest {
        attributes 'Main-Class': "$mainClassName"
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task deploy(dependsOn: [jar, test]) {
}